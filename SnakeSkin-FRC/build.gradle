dependencies {
    implementation group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: '2018.1.1'
    implementation project(':SnakeSkin-Core')
    kapt project(':SnakeSkin-Core')
}

sourceSets {
    patches {
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

jar {
    from (sourceSets.patches.output) {
        rename '(.*).class', '$1.class.patch'
        into 'patches'
    }
}

publishing {
    publications {
        Frc(MavenPublication) {
            from components.java
            groupId 'org.snakeskin'
            artifactId 'SnakeSkin-FRC'
            artifact sourcesJar
            artifact javadocJar
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    publications = ['Frc']
    pkg {
        repo = "SnakeSkin"
        name = "SnakeSkin-FRC"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/snakeskin"
        version {
            name = this.version
        }
    }
}