dependencies {
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    implementation group: 'com.squareup', name: 'javapoet', version: '1.11.1'
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.3'
    implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.4.12' //It was Duke's Choice!
    api group: 'org.snakeskin', name: 'SnakeSkin-Units', version: '1.0'
}

publishing {
    publications {
        Core(MavenPublication) {
            from components.java
            groupId 'org.snakeskin'
            artifactId 'SnakeSkin-Core'
            artifact sourcesJar
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    publications = ['Core']
    pkg {
        repo = "SnakeSkin"
        name = "SnakeSkin-Core"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/snakeskin"
        version {
            name = this.version
        }
    }
}

jar {
    doFirst {
        def versionFile = file("$buildDir/META-INF/snakeskin-version.properties")
        versionFile.parentFile.mkdirs()
        if (versionFile.exists()) versionFile.delete()
        versionFile.createNewFile()

        versionFile << "core=$version"
    }

    from fileTree(buildDir).matching { include "META-INF/snakeskin-version.properties"}
}