plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.61" apply false
    id "com.palantir.git-version" version "0.10.0"
    id "com.jfrog.bintray" version "1.8.4" apply false
}

allprojects {
    apply plugin: 'idea'
}

ext.wpilibVersion = "2020.1.2"

subprojects { subproj ->
    apply plugin: 'java-library'
    apply plugin: 'kotlin'

    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    group 'org.snakeskin'
    version gitVersion()

    repositories {
        mavenCentral()

        //401 maven
        maven {
            url "http://maven.team401.org/artifactory/frc"
        }

        //SnakeSkin bintray
        maven {
            url "http://dl.bintray.com/team401/SnakeSkin"
        }
        //WPI Maven (new)
        maven {
            url "https://frcmaven.wpi.edu/artifactory/release"
        }
        //WPI Maven (old)
        maven {
            url "http://first.wpi.edu/FRC/roborio/maven/release/"
        }
        //CTRE Maven
        maven  {
            url "http://devsite.ctr-electronics.com/maven/release/"
        }
        //REV Maven
        maven {
            url "http://www.revrobotics.com/content/sw/max/sdk/maven/"
        }
    }

    //Jars built for github releases
    jar {
        destinationDirectory = file("$rootDir/out/libs")
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
}

wrapper {
    gradleVersion = '6.0.1'
}
